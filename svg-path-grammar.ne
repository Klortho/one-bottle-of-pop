# This is the EBNF grammar for the SVG mini-language used to specify
# paths. This is translated from the form in the SVG spec
# (https://www.w3.org/TR/SVG/paths.html#PathDataBNF) into the syntax
# expected by nearly (https://github.com/Hardmath123/nearley).
# Try it at https://omrelli.ug/nearley-playground/

SVG_PATH -> WSP:* MOVETO_DRAWTO_CMD_GROUPS:? WSP:*
WSP -> " " | "\t" | "\n" | "\r"
MOVETO_DRAWTO_CMD_GROUPS -> MOVETO_DRAWTO_CMD_GROUP
    | MOVETO_DRAWTO_CMD_GROUP WSP:* MOVETO_DRAWTO_CMD_GROUPS
MOVETO_DRAWTO_CMD_GROUP -> MOVETO WSP:* DRAWTO_CMDS:?
DRAWTO_CMDS -> DRAWTO_CMD
    | DRAWTO_CMD WSP:* DRAWTO_CMDS
DRAWTO_CMD -> CLOSEPATH
    | LINETO
	| HORIZ_LINETO
	| VERT_LINETO
	| CURVETO
	| SMOOTH_CURVETO
	| QBEZIER_CURVETO
	| SMOOTH_QBEZIER_CURVETO
	| ARC
MOVETO -> ( "M" | "m" ) WSP:* MOVETO_ARG_SEQ
MOVETO_ARG_SEQ -> COORD_PAIR
    | COORD_PAIR COMMA_WSP:? LINETO_ARG_SEQ
CLOSEPATH -> ("Z" | "z")
LINETO -> ("L" | "l") WSP:* LINETO_ARG_SEQ
LINETO_ARG_SEQ -> COORD_PAIR
    | COORD_PAIR COMMA_WSP:? LINETO_ARG_SEQ
HORIZ_LINETO -> ("H"|"h") WSP:* HORIZ_LINETO_ARG_SEQ
HORIZ_LINETO_ARG_SEQ -> COORD
    | COORD COMMA_WSP:? HORIZ_LINETO_ARG_SEQ
VERT_LINETO -> ("V"|"v") WSP:* VERT_LINETO_ARG_SEQ
VERT_LINETO_ARG_SEQ -> COORD
    | COORD COMMA_WSP:? VERT_LINETO_ARG_SEQ
CURVETO -> ("C"|"c") WSP:* CURVETO_ARG_SEQ
CURVETO_ARG_SEQ -> CURVETO_ARG
    | CURVETO_ARG COMMA_WSP:? CURVETO_ARG_SEQ
CURVETO_ARG -> COORD_PAIR COMMA_WSP:? COORD_PAIR COMMA_WSP:? COORD_PAIR
SMOOTH_CURVETO -> ("S"|"s") WSP:* SMOOTH_CURVETO_ARG_SEQ
SMOOTH_CURVETO_ARG_SEQ -> SMOOTH_CURVETO_ARG
    | SMOOTH_CURVETO_ARG COMMA_WSP:? SMOOTH_CURVETO_ARG_SEQ
SMOOTH_CURVETO_ARG -> COORD_PAIR COMMA_WSP:? COORD_PAIR
QBEZIER_CURVETO -> ("Q"|"q") WSP:* QBEZIER_CURVETO_ARG_SEQ
QBEZIER_CURVETO_ARG_SEQ -> QBEZIER_CURVETO_ARG
    | QBEZIER_CURVETO_ARG COMMA_WSP:? QBEZIER_CURVETO_ARG_SEQ
QBEZIER_CURVETO_ARG -> COORD_PAIR COMMA_WSP:? COORD_PAIR
SMOOTH_QBEZIER_CURVETO -> ("T"|"t") WSP:* SMOOTH_QBEZIER_CURVETO_ARG_SEQ
SMOOTH_QBEZIER_CURVETO_ARG_SEQ -> COORD_PAIR
    | COORD_PAIR COMMA_WSP:? SMOOTH_QBEZIER_CURVETO_ARG_SEQ
ARC -> ("A"|"a") WSP:* ARC_ARG_SEQ
ARC_ARG_SEQ -> ARC_ARG
    | ARC_ARG COMMA_WSP:? ARC_ARG_SEQ
ARC_ARG -> NONNEG_NUMBER COMMA_WSP:? NONNEG_NUMBER COMMA_WSP:? NUMBER COMMA_WSP:? FLAG COMMA_WSP:? FLAG COMMA_WSP:? COORD_PAIR



COORD_PAIR -> COORD COMMA_WSP:? COORD
COORD -> NUMBER
NONNEG_NUMBER -> INT | FLOAT
NUMBER -> SIGN:? INT
    | SIGN:? FLOAT
FLAG -> "0"|"1"
COMMA_WSP -> (WSP:+ ",":? WSP:*) | ("," WSP:*)
INT -> DIGITS
FLOAT -> FRACTION EXP:?
    | DIGITS EXP
FRACTION -> DIGITS:? "." DIGITS
    | DIGITS "."
EXP -> ("E"|"e") SIGN:? DIGITS
SIGN -> "+"|"-"
DIGITS -> [0-9]:+
WSP -> (" "|"\t"|"\n"|"\r")
